#!/usr/bin/perl

#
#  objc-rolec
#  ArchRole
#
#  Created by Brent Royal-Gordon on 7/19/11.
#  Copyright 2011 Architechies. All rights reserved.
#

use strict;
use warnings;

my($infile, $outfile) = @ARGV;

unless(defined $outfile or $infile =~ /\.h$/) {
    ($outfile = $infile) =~ s/\.\w+$/.h/;
}

unless(defined $outfile and defined $infile) {
    print STDERR <<"END";
Usage:  $0 MyRole.rh MyRole.h
        $0 MyRole.rh            # Strips extension, adds .h
END
    exit(1);
}

use File::Slurp qw(slurp write_file);

$_ = slurp $infile;
	
s{
    \@role \s+ (\w+) \s*;
}{\@class $1; \@protocol $1;}xg;

s{
    \@role \s+ (\w+) \s* (?: < ([^>]*) > )? 	# $1 = role name, $2 = super-roles
    ( (?: [^@] | @(?!provides) )* )				# $3 = requirements
    \@provides
    ( (?: [^@] | @(?!end) )* )					# $4 = provided
    \@end
}{/* BEGIN Role $1 -- generated by $0 */
\@protocol $1 <@{[ $2 ? (join ', ', "ArchRole", split ',', $2) : 'ArchRole' ]}>

\@required
$3

\@optional
$4

\@end

\@interface $1 : ArchRole

$4

\@end

\@interface $1 (ObjCRoleC_ComingFromElsewhere) @{[ $2 ? "<$2>" : "" ]}

$3

\@end
/* END Role $1 -- generated by $0 */}xg;
	
write_file $outfile, $_;
